plugins { id("com.google.cloud.tools.jib") version "3.2.0" }

ext {
	APP_CORE = ":app-core"
	WEB_CORE = ":web-core"
}

def queryDslVersion = dependencyManagement.importedProperties["querydsl.version"]

dependencies {
	implementation project(APP_CORE)

	implementation("com.querydsl:querydsl-jpa:$queryDslVersion:jakarta")
	annotationProcessor("com.querydsl:querydsl-apt:$queryDslVersion:jakarta")
	annotationProcessor("jakarta.annotation:jakarta.annotation-api")
	annotationProcessor("jakarta.persistence:jakarta.persistence-api")

	testImplementation project(path: APP_CORE, configuration: "archives")
	testImplementation project(path: WEB_CORE, configuration: "archives")
	testRuntimeOnly("com.h2database:h2")
}

jib {
	from {
		image = "openjdk:17-jdk-slim"
	}
	to {
		image = "${project.name}:latest"
	}
	container {
		mainClass = "io.fulflix.product.ProductApp"
		creationTime = "USE_CURRENT_TIMESTAMP"
	}
}

// query-dsl q-class generate and delete configurations
tasks.withType(JavaCompile).configureEach {
	options.generatedSourceOutputDirectory = file('src/main/generated')
	options.compilerArgs += [
			"-Aquerydsl.generatedAnnotationClass=javax.annotation.Generated"
	]
}

tasks.named('clean', Delete).configure {
	delete file('src/main/generated')
}
